<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.0" halcon_version="10.0.1" cs="750153789">
<procedure name="init_camera">
<interface>
<oo>
<par name="UnDistMap"/>
</oo>
<ic>
<par name="SelectedIndex"/>
</ic>
<oc>
<par name="AcqHandle"/>
<par name="ToolPoseInCam"/>
<par name="CamParam"/>
</oc>
</interface>
<body>
<l>DataPath := '../Data/'</l>
<l>AcqHandle := []</l>
<c></c>
<c>*NAXTAGEのデフォルトデータパスを取得</c>
<l>NextageName := 'NEXTAGE'</l>
<l>tuple_environment ('APPDATA', appDataPath)</l>
<l>tuple_environment ('NEXTAGE_SDK', nxPath)</l>
<l>if (nxPath = '')</l>
<l>    version := 'v21'</l>
<l>else</l>
<l>    tmp := split(nxPath, '\\')</l>
<l>    version := tmp[|tmp|-1]</l>
<l>endif</l>
<l>CamParamPath := appDataPath + '/' + NextageName + '/' + version + '/CamParams/'</l>
<l>CameraSettingsFilename := CamParamPath + 'CameraSettings.dat'</l>
<c></c>
<l>if (SelectedIndex # 5)</l>
<c>    *</c>
<l>    close_all_framegrabbers()</l>
<l>    if (SelectedIndex # 4)</l>
<l>        init_framegrabbers (CameraSettingsFilename, SelectedIndex, FGHandles, FGStateStrings)</l>
<l>        AcqHandle := FGHandles[SelectedIndex]</l>
<l>    else</l>
<l>        init_framegrabbers (CameraSettingsFilename, [0,1], FGHandles, FGStateStrings)</l>
<l>        AcqHandle := FGHandles[0:1]</l>
<l>    endif</l>
<l>    if (AcqHandle &lt;= 0)</l>
<l>        close_all_framegrabbers()</l>
<l>        init_framegrabbers (CameraSettingsFilename, 'all', FGHandles, FGStrings)</l>
<l>        dev_inspect_ctrl(FGStateStrings)</l>
<l>        stop()</l>
<l>    endif</l>
<c>    *</c>
<l>    if (SelectedIndex = 0 or SelectedIndex = 1 or SelectedIndex = 4)</l>
<l>        CamParamFilenameL := CamParamPath + 'CamParam_Interior_Raw_0.campar'</l>
<l>        CamParamFilenameR := CamParamPath + 'CamParam_Interior_Raw_1.campar'</l>
<l>        RelPoseFilename := CamParamPath + 'RelPoseInCam_Raw_0_to_1.pose'</l>
<l>        ToolPoseInCamFilename := CamParamPath + 'ToolPoseInCam_StereoRect_0.pose'</l>
<l>        read_cam_par (CamParamFilenameL, CamParamInL)</l>
<l>        read_cam_par (CamParamFilenameR, CamParamInR)</l>
<l>        read_pose (RelPoseFilename, RelPoseIn)</l>
<l>        read_pose (ToolPoseInCamFilename, ToolPoseInCam)</l>
<c>        *</c>
<l>        gen_binocular_rectification_map(UnDistMapL, UnDistMapR, CamParamInL, CamParamInR, RelPoseIn, 1, 'geometric', 'bilinear', CamParamL, CamParamR, CamPoseL, CamPoseR, RelPose)</l>
<l>        if (SelectedIndex = 0)</l>
<l>            copy_obj (UnDistMapL, UnDistMap, 1, -1)</l>
<l>            CamParam := CamParamL</l>
<l>            ToolPoseInCam := CamPoseL</l>
<l>        elseif (SelectedIndex = 1)</l>
<l>            copy_obj (UnDistMapR, UnDistMap, 1, -1)</l>
<l>            CamParam := CamParamR</l>
<l>            ToolPoseInCam := CamPoseR</l>
<l>        else</l>
<l>            concat_obj (UnDistMapL, UnDistMapR, UnDistMap)</l>
<l>            CamParam := [CamParamL, CamParamR]</l>
<l>            ToolPoseInCam := [CamPoseL, CamPoseR]</l>
<l>        endif</l>
<l>    elseif (SelectedIndex = 2 or SelectedIndex = 3)</l>
<l>        CamParamFilename := CamParamPath + 'CamParam_Interior_Raw_' + SelectedIndex + '.campar'</l>
<l>        ToolPoseInCamFilename := CamParamPath + 'ToolPoseInCam_Raw_' + SelectedIndex + '.pose'</l>
<l>        read_pose (ToolPoseInCamFilename, ToolPoseInCam)</l>
<l>        read_cam_par (CamParamFilename, CamParamIn)</l>
<l>        tuple_gen_const (|CamParamIn| - 7, 0, zeros)</l>
<l>        change_radial_distortion_cam_par ('preserve_resolution', CamParamIn, zeros, CamParam)</l>
<l>        gen_radial_distortion_map (UnDistMap, CamParamIn, CamParam, 'bilinear')</l>
<l>    endif</l>
<l>elseif (SelectedIndex = 5)</l>
<l>    dev_inspect_ctrl(DataPath)</l>
<l>    stop()</l>
<l>    dev_update_var('on')</l>
<l>    CamParamFilename := DataPath + 'CamParam_Interior_Raw_0.campar'</l>
<l>    ToolPoseInCamFilename := DataPath + 'ToolPoseInCam_StereoRect_0.pose'</l>
<l>    dev_inspect_ctrl([CamParamFilename, ToolPoseInCamFilename])</l>
<l>    stop()</l>
<l>    read_cam_par (CamParamFilename, CamParamIn)</l>
<l>    read_pose (ToolPoseInCamFilename, ToolPoseInCam)</l>
<l>    tuple_gen_const (|CamParamIn| - 7, 0, zeros)</l>
<l>    change_radial_distortion_cam_par ('preserve_resolution', CamParamIn, zeros, CamParam)</l>
<l>    gen_radial_distortion_map (UnDistMap, CamParamIn, CamParam, 'bilinear')</l>
<l>endif</l>
<c></c>
<l>return ()</l>
</body>
<docu id="init_camera">
<parameters>
<parameter id="AcqHandle"/>
<parameter id="CamParam"/>
<parameter id="SelectedIndex"/>
<parameter id="ToolPoseInCam"/>
<parameter id="UnDistMap"/>
</parameters>
</docu>
</procedure>
</hdevelop>
